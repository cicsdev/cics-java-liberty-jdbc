plugins
{
id 'java'
id 'eclipse'
id 'idea'
id 'war'
id 'com.ibm.cics.bundle' version '1.0.6'
}


// Set source directory for Java build
sourceSets {
    main {
        java {
            srcDirs 'projects'
        }
    }
}


group = 'com.ibm.cicsdev.jdbc'
archivesBaseName='cics-java-liberty-jdbc'
version = '0.1.0'
sourceCompatibility = '1.8'

// If in Eclipse, add Javadoc to the local project classpath
eclipse 
{
    classpath 
    {
        downloadJavadoc = true
    }
}

repositories 
{
    mavenCentral()
}

dependencies 
{      
    // CICS TS V5.5 Maven BOM (as of 19th May 2020)
    compileOnly enforcedPlatform("com.ibm.cics:com.ibm.cics.ts.bom:5.5-20200519131930-PH25409")

    // Don't include JCICS in the final build (no need for version because we have BOM)
    compileOnly("com.ibm.cics:com.ibm.cics.server")     

    // Compile against, but don't include Java EE components in the final build 
    compileOnly ("javax.servlet:javax.servlet-api:3.1.0")    

    // Add the files from the war task as CICS bundle parts
    cicsBundlePart files(war)
}

// CICS bundle build, CICS bundle build, specify defaultJVMServer value to match CICS JVMSERVER name
cicsBundle {
    build {
         defaultJVMServer = project.findProperty("jvmserver") ?: "DFHWLP"
    }
}
